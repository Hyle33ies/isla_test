<start>                 ::= <cypher_query>
<cypher_query>          ::= <statement> | <statement> "; " <cypher_query>
<statement>             ::= <create_clause> <modify_clause_opt> "; "<match_return_clause_opt>
<modify_clause_opt>     ::= <delete_clause> | <set_clause> | ""
<delete_clause>         ::= " DELETE " <delete_items>
<set_clause>            ::= " SET " <set_items>
<delete_items>          ::= <node_variable> | <node_variable> ", " <delete_items>
<set_items>             ::= <set_item> | <set_item> ", " <set_items>
<set_item>              ::= <node_variable> " = " <number>
<match_return_clause_opt> ::= <match_clause> <return_clause> | ""
<match_clause>          ::= " MATCH " <pattern>
<pattern>               ::= <node_pattern> | <node_pattern> <relationship_pattern> <node_pattern>
<node_pattern>          ::= "(" <node_variable> <label_opt> <properties> ")"
<label_opt>             ::= ": " <label> | ""
<relationship_pattern>  ::= "-[" <relation_variable> ":" <type> <properties> "]->"
<create_clause>         ::= " CREATE " <pattern>
<return_clause>         ::= " RETURN " <return_items>
<return_items>          ::= <return_item> | <return_item> ", " <return_items>
<return_item>           ::= <node_variable>
<properties>            ::= "{" <property_list> "}"
<property_list>         ::= <property> | <property> "," <property_list>
<property>              ::= <property_string> ":" <number>
<node_variable>         ::= "n" <digits>
<label>                 ::= "l" <digits>
<type>                  ::= "r" <digits>
<property_string>       ::= "T" <digits>
<number>                ::= <digit> <digits_opt>
<digits_opt>            ::= <digit> | <digit> <digits_opt> | ""
<digits>                ::= <digit> | <digits> <digit>
<digit>                 ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<relation_variable>     ::= "r" <digits>
